plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
}

//Loading Keystore specified for SE Access
Properties properties = new Properties()
if (project.rootProject.file("local.properties").exists()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
}

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "org.eclipse.keyple.plugin.android.omapi.example"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }


    signingConfigs {
        if (properties.containsKey("path_to_se_registered_keystore_cna")) {
            keypleDebug {
                storeFile file(properties["path_to_se_registered_keystore_cna"])
                storePassword properties["store_password_cna"]
                keyAlias properties["key_alias_cna"]
                keyPassword properties["key_password_cna"]
            }
        }
    }

    buildTypes {
        if (properties.containsKey("path_to_se_registered_keystore_cna")) {
            debug {
                signingConfig signingConfigs.keypleDebug
                debuggable true
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
    packagingOptions {
        exclude "META-INF/NOTICE.md"
    }
    kotlinOptions {
        jvmTarget = '1.6'
    }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }
}

dependencies {

    implementation fileTree(include: ['*.jar'], exclude: ['org.simalliance.openmobileapi.jar'], dir: 'libs')

    //Keyple
    implementation project(path: ':keyple-plugins:android-omapi')
    implementation("org.calypsonet.terminal:calypsonet-terminal-reader-java-api:1.0-SNAPSHOT")
    implementation "org.eclipse.keyple:keyple-service-java-lib:2.0.0-SNAPSHOT"
    //For Generic Extension examples
    implementation "org.eclipse.keyple:keyple-card-generic-java-lib:2.0.0-SNAPSHOT"

    //TEST
    //keyple
    implementation("org.eclipse.keyple:keyple-common-java-api:2.0-SNAPSHOT")
    implementation("org.eclipse.keyple:keyple-plugin-java-api:2.0-SNAPSHOT")
    implementation("org.eclipse.keyple:keyple-util-java-lib:2.0.0-SNAPSHOT")

    //



    /*
    Android components
    */
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.activity:activity-ktx:1.2.1"
    implementation 'androidx.fragment:fragment-ktx:1.3.1'

    /*
    Log
    */
    implementation "org.slf4j:slf4j-api:1.7.30"
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.arcao:slf4j-timber:3.1@aar' //SLF4J binding for Timber

    /*
    Kotlin
    */
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    /*
    Coroutines
    */
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'

    testImplementation "junit:junit:4.12"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}